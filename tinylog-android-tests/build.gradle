apply plugin: "checkstyle"
apply plugin: "com.android.library"
apply plugin: "com.github.spotbugs"
apply plugin: "jacoco"
apply plugin: "de.mannodermaus.android-junit5"

buildscript {
    repositories {
        google()
        gradlePluginPortal()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.3.1"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.2"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.10.0.0"
        classpath "org.jacoco:org.jacoco.core:0.8.11"
    }
}

repositories {
    google()
    gradlePluginPortal()
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.tinylog:tinylog-core:${version}"
    implementation "org.tinylog:tinylog-impl:${version}"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.4.0"
    androidTestImplementation "javax.inject:javax.inject:1"
    androidTestImplementation "org.assertj:assertj-core:3.25.3"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-params:5.10.2"
    androidTestImplementation "org.mockito:mockito-android:5.11.0"
    androidTestImplementation "org.tinylog:tinylog-core:${version}:tests"
    androidTestImplementation "org.tinylog:tinylog-impl:${version}:tests"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.4.0"
}

android {
    namespace "tinylog"
    buildToolsVersion "30.0.3"

    sourceSets {
        main {
            java {
                srcDirs += "../tinylog-core/src/main/java"
                srcDirs += "../tinylog-impl/src/main/java"
                exclude "**/JavaRuntime.java", "**/JdbcWriter*.java", "**/JndiValueResolver.java"
            }
        }
    }

    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    packagingOptions {
        exclude("META-INF/LICENSE*")
        exclude("META-INF/licenses/*")
    }
}

spotbugs {
    excludeFilter = file("../configuration/spotbugs-filters.xml")
}

task checkstyle(type: Checkstyle) {
    configFile file("../configuration/checkstyle-rules.xml")
    configProperties["workspace_loc"] = file("..").path
    source "src"
    classpath = files()
}
